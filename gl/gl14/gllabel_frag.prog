!!ARBfp1.0
#
# Fragment program used for rendering GLLabel instances.

TEMP  voxCoord;
TEMP  lutCoord;
TEMP  invNumLabels;
TEMP  voxValue;

PARAM imageShape    = program.local[4];
MOV   invNumLabels,   program.local[5];
PARAM outline       = program.local[6];

# This matrix scales the voxel value to
# lie in a range which is appropriate to
# the current display range 
PARAM voxValXform[4] = { program.local[0],
                         program.local[1],
                         program.local[2],
                         program.local[3] };

# retrieve the voxel coordinates,
# bail if they are are out of bounds  
MOV voxCoord, fragment.texcoord[1];

#pragma include test_in_bounds.prog

# look up image voxel value
# from 3D image texture
TEX voxValue, fragment.texcoord[0], texture[0], 3D;

# Scale the texture value
# to its original voxel value
MOV lutCoord, voxValue;
MAD lutCoord, lutCoord, voxValXform[0].x, voxValXform[3].x;

# Scale the voxel value to
# a lut texture coordinate
ADD lutCoord, lutCoord, { 0.5, 0, 0, 0 };
MUL lutCoord, lutCoord, invNumLabels;

# look up the appropriate colour
# in the 1D colour map texture
TEX result.color, lutCoord.x, texture[1], 1D;


# Test whether this fragment lies 
# on an edge between label regions.

TEMP coord;
TEMP val;
TEMP tol;
TEMP offsets;

MOV coord, fragment.texcoord[0];
MOV val,   voxValue;
MOV tol,   invNumLabels.x;
MUL tol,   tol, 0.001;

MOV offsets, outline.yzww;

#pragma include edge3D.prog


# Figure out if we want to fill the
# fragment, or kill the fragment.
TEMP fill;

# Set fill to 1 if there is an edge 
# along any dimension, 0 otherwise
MAX fill, isEdge.x, isEdge.y;
MAX fill, fill,     isEdge.z;

# 
# If outlines are enabled, and
# this fragment is not on an edge,
# kill the fragment.
# 
# Simiarly, if outlines are disabled
# and this fragment is on an edge,
# kill the fragment.
#
# This boils down to testing whether
# isEdge == outline.x
# 
SUB fill, fill, outline.x;
ABS fill, fill;

# Fill is now 0 if isEdge == outline.x,
# 1 otherwise

MAD fill, fill, -1, 0.5;
KIL fill;

  
END
